import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'jvm-test-suite'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.3'
}

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom 'io.cucumber:cucumber-bom:7.14.0'
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
            }
        }

        acceptanceTest(JvmTestSuite) {
            useJUnitJupiter()

            dependencies {
                implementation testFixtures(project(':scenarios'))

                implementation 'org.junit.platform:junit-platform-suite-api'
                implementation 'org.junit.platform:junit-platform-suite-engine'
                implementation 'io.cucumber:cucumber-junit-platform-engine'
            }
        }
    }
}

acceptanceTest.configure {
    shouldRunAfter(test)
}

tasks.named('check') {
    dependsOn(testing.suites.acceptanceTest)
}
